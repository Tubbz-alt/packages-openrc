# Maintainer: artoo <artoo@manjaro.org>

pkgname=elogind
pkgver=226
pkgrel=1
pkgdesc="The systemd project's logind, extracted to a standalone package"
arch=('i686' 'x86_64')
url="https://github.com/elogind/elogind"
license=('GPL')
provides=("elogind=${pkgver}")
depends=('dbus' 'eudev-systemd')
makedepends=('libcap' 'intltool' 'libtool' 'gperf' 'gtk-doc' 'libseccomp' 'eudev')
optdepends=('polkit-elogind: Polkit elogind session tracking'
            'elogind-openrc: elogind openrc initscript')
conflicts=('systemd-sysvcompat' 'consolekit')
options=('!libtool')
install=elogind.install
_branch=dev_v${pkgver}
source=("elogind-${pkgver}.tar.gz::${url}/archive/${_branch}.tar.gz"
        "elogind-docs.patch")
sha256sums=('165cae0af29ab027f5b1d8286066309aa34d8baa94a5f5e62e8f576b4e43a334'
            '25643e563250b0d322f817288cbe999f1cc2e4804c875725bb92fe5094a902b4')

prepare() {
    cd ${srcdir}/elogind-${_branch}
    patch -Np 1 -i ${srcdir}/elogind-docs.patch
    autoreconf -vim
}

build() {
    cd ${srcdir}/elogind-${_branch}
    intltoolize
    ./configure \
        --sysconfdir=/etc \
        --prefix=/usr \
        --with-rootprefix=/usr \
        --enable-split-usr \
        --enable-smack \
        --disable-kdbus \
        --libexecdir=/usr/lib

    make
}

package() {
    cd ${srcdir}/elogind-${_branch}
    make DESTDIR="${pkgdir}" install
}
