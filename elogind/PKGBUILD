# Maintainer: artoo <artoo@manjaro.org>

_pkgname=elogind
_commit=9226293c85ae14b23a41657c59edd8b22570e532

pkgbase=elogind
pkgname=('elogind' 'libelogind')
pkgver=226
pkgrel=4
pkgdesc="The systemd project's logind, extracted to a standalone package"
arch=('i686' 'x86_64')
url="https://github.com/elogind/elogind"
license=('GPL')
makedepends=('intltool' 'libtool' 'gperf' 'gtk-doc' 'dbus-elogind' 'git' 'libcap')
options=('!libtool')
source=("elogind-${_commit}.tar.gz::${url}/archive/${_commit}.tar.gz"
        "elogind-docs.patch")
sha256sums=('65b018fb6bd28b3d66f2a45b59ceacea5e0514427c7754a6b439f9668b4342d1'
            '25643e563250b0d322f817288cbe999f1cc2e4804c875725bb92fe5094a902b4')

prepare() {
    cd ${_pkgname}-${_commit}
    patch -Np 1 -i ${srcdir}/elogind-docs.patch
    autoreconf -vim
}

build() {
    cd ${_pkgname}-${_commit}
    intltoolize
    ./configure \
        --sysconfdir=/etc \
        --prefix=/usr \
        --enable-split-usr \
        --enable-smack \
        --disable-kdbus \
        --with-rootlibdir=/usr/lib \
        --with-udevrulesdir=/usr/lib/udev/rules.d \
        --libexecdir=/usr/lib

    make
}

package_elogind() {
    pkgdesc="The systemd project's logind, extracted to a standalone package"
    provides=("elogind=${pkgver}")
    depends=('libelogind' 'pam' 'acl' 'dbus-elogind')
    conflicts=('systemd-sysvcompat' 'consolekit')
    # replaces=('consolekit')
    optdepends=('elogind-openrc: elogind initscript'
                'dbus-openrc: dbus initscript'
                'polkit-elogind: polkit with elogind session tracking')
    install=elogind.install

    cd ${_pkgname}-${_commit}
    make DESTDIR="${pkgdir}" install

    mkdir ${srcdir}/_libelogind
    mv -v ${pkgdir}/usr/lib/pkgconfig ${srcdir}/_libelogind
	mv -v ${pkgdir}/usr/include/ ${srcdir}/_libelogind
	mv -v ${pkgdir}/usr/lib/libelogind*.so* ${srcdir}/_libelogind
}

package_libelogind(){
    pkgdesc="elogind client libraries"
    provides=('libelogind.so' "libelogind=${pkgver}")
    depends=('libcap')

    cd ${_pkgname}-${_commit}
    install -dm755 ${pkgdir}/usr/lib
    mv ${srcdir}/_libelogind/include ${pkgdir}/usr
    mv ${srcdir}/_libelogind/pkgconfig ${pkgdir}/usr/lib
    mv ${srcdir}/_libelogind/libelogind*.so* ${pkgdir}/usr/lib
}
