# Maintainer: artoo <artoo@manjaro.org>

_pkgname=elogind
_commit=b05d25419669a6675a88c5f305a8a7e0a7908cb3

pkgname=elogind
pkgver=226
pkgrel=2
pkgdesc="The systemd project's logind, extracted to a standalone package"
arch=('i686' 'x86_64')
url="https://github.com/elogind/elogind"
license=('GPL')
provides=("elogind=${pkgver}")
depends=('dbus' 'eudev-systemd')
makedepends=('libcap' 'intltool' 'libtool' 'gperf' 'gtk-doc' 'libseccomp' 'eudev' 'git')
optdepends=('polkit-elogind: polkit with elogind session tracking'
            'dbus-openrc: dbus initscript'
            'elogind-openrc: elogind initscript')
conflicts=('systemd-sysvcompat' 'consolekit')
options=('!libtool')
install=elogind.install
source=("elogind-${_commit}.tar.gz::${url}/archive/${_commit}.tar.gz"
        "elogind-docs.patch")
sha256sums=('ab4b8aed435b3609bd1ef5dcf8a2737b22a8c1c7b2f4132c0a3478dcd3348596'
            '25643e563250b0d322f817288cbe999f1cc2e4804c875725bb92fe5094a902b4')

prepare() {
    cd ${_pkgname}-${_commit}
    patch -Np 1 -i ${srcdir}/elogind-docs.patch
    autoreconf -vim
}

build() {
    cd ${_pkgname}-${_commit}
    intltoolize
    ./configure \
        --sysconfdir=/etc \
        --prefix=/usr \
        --enable-split-usr \
        --enable-smack \
        --disable-kdbus \
        --libexecdir=/usr/lib

    make
}

package() {
    cd ${_pkgname}-${_commit}
    make DESTDIR="${pkgdir}" install
}
