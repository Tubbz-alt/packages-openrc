# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=sddm

pkgname=sddm-elogind
pkgver=0.14.0
pkgrel=1
pkgdesc='QML based X11 display manager'
arch=('i686' 'x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
provides=('display-manager' 'sddm')
depends=('qt5-declarative' 'dbus' 'elogind')
makedepends=('extra-cmake-modules' 'python-docutils' 'qt5-tools')
optdepends=('dbus-openrc: dbus initscript'
            'displaymanager-openrc: displaymanager initscript')
conflicts=('display-manager' 'sddm' 'sddm-consolekit')
install="${_pkgname}.install"
backup=('usr/share/sddm/scripts/Xsetup'
        'etc/pam.d/sddm'
        'etc/sddm.conf')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz"
        "sddm-fix-avatars.patch::https://github.com/sddm/sddm/commit/ecb903e4.patch"
        'sddm-elogind-pam.patch')
sha256sums=('a551551a6ba324e9c384c89bc63e871de65fea3740eadbea2d63df86045f8205'
            'b091f02d6d5b2ddc9d5208b17213a181c6e8deae23e5633d694119e79b66d38b'
            '0cbaf41fc677ec1ef3e120e351a09ebc875ffc6b92b98a7270f58d2fbe7eddf3')

prepare() {
	mkdir -p build
	cd ${_pkgname}-${pkgver}
	patch -p1 -i ${srcdir}/sddm-elogind-pam.patch
}

build() {
	cd build
	cmake ../${_pkgname}-${pkgver} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
		-DDBUS_CONFIG_FILENAME=org.freedesktop.sddm.conf \
		-DBUILD_MAN_PAGES=ON
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install

# 	sed -e 's|systemctl reboot|shutdown -r now|' \
#         -e 's|systemctl poweroff|shutdown -h -P|' \
#         -e 's|MinimumVT=1|MinimumVT=7|' \
#         -i ${pkgdir}/etc/sddm.conf
}
