# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgname=libmbim

pkgname=libmbim-nosystemd
pkgver=1.14.0
pkgrel=2
pkgdesc="MBIM modem protocol helper library"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/libmbim/"
license=(GPL2)
depends=(glib2 bash eudev libgudev-nosystemd)
makedepends=(gtk-doc python git)
provides=("${_pkgname}")
conflicts=("${_pkgname}")
_commit=50cddf48823f05ec124afad80c0d948184e19b00  # tags/1.14.0
source=("git://anongit.freedesktop.org/libmbim/libmbim#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$_pkgname --disable-static --enable-gtk-doc
  make
}

check() {
  cd $_pkgname
  make check
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
}
