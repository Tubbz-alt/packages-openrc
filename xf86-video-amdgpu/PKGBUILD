# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

_pkgname=xf86-video-amdgpu

pkgname=xf86-video-amdgpu-nosystemd
pkgver=1.2.0
pkgrel=2
pkgdesc="X.org amdgpu video driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libudev.so' 'mesa')
makedepends=('xorg-server-devel' 'udev' 'X-ABI-VIDEODRV_VERSION=23')
provides=("$_pkgname=$pkgver")
conflicts=('xorg-server<1.19.0' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24' "$_pkgname")
# groups=('xorg-drivers')
source=(${url}/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2{,.sig})
md5sums=('f4e335b580336e03eeef10b08b68d757'
         'SKIP')
validpgpkeys=('B09FAF35BE914521980951145A81AF8E6ADBB200') # Michel Daenzer <michel@daenzer.net>

build() {
  cd ${_pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --enable-glamor
  make
}

package() {
  cd ${_pkgname}-${pkgver}

  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
