# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=udisks2-elogind
pkgver=2.6.4
pkgrel=1
pkgdesc="Disk Management Service, version 2"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/udisks"
license=('GPL2')
groups=('elogind-nosystemd')
depends=('glib2' 'polkit-elogind' 'libatasmart-nosystemd' 'libgudev-nosystemd' 'eudev')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'gnome-common')
optdepends=('parted: partition management'
            'gptfdisk: GUID partition table support'
            'ntfs-3g: NTFS filesystem management support'
            'dosfstools: VFAT filesystem management support')
provides=("udisks2=${pkgver}")
conflicts=('udisks2')
source=("udisks2-${pkgver}::https://github.com/storaged-project/udisks/archive/udisks-${pkgver}.tar.gz"
        "udisks-enable-elogind.patch::https://github.com/udeved/udisks/commit/123feff78bc5d596e0663ccfccdbdb5d9ebbcb7e.patch")
sha256sums=('7c7889dbd8f910b8a8094841168c04d48e0bdaf946b22aa5f0742a460473c191'
            'ec3921dd046de04438e0c2ad6d476223e7c1e490ace0515498b47db3a45d0ded')

prepare(){
    cd "udisks-udisks-$pkgver"
    sed -i "s:udisksprivdir:udisksdprivdir:" data/Makefile.am
    patch -Np 1 -i ${srcdir}/udisks-enable-elogind.patch
}

build() {
  cd "udisks-udisks-$pkgver"
  ./autogen.sh \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --sbindir=/usr/bin \
      --libexecdir=/usr/lib
  make
}

check() {
  cd udisks-udisks-$pkgver
  make check
}

package() {
  cd "udisks-udisks-$pkgver"
  make DESTDIR="$pkgdir" install \
    bash_completiondir=/usr/share/bash-completion/completions
}
