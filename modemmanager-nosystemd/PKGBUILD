# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgname=modemmanager

pkgname=modemmanager-nosystemd
pkgver=1.6.4
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=(i686 x86_64)
url="https://www.freedesktop.org/wiki/Software/ModemManager/"
license=(GPL2 LGPL2.1)
depends=(eudev libgudev-nosystemd polkit-elogind ppp libqmi libmbim-nosystemd libmm-glib)
makedepends=(intltool gtk-doc gobject-introspection vala autoconf-archive git)
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
options=(!emptydirs)
provides=("${_pkgname}")
conflicts=("${_pkgname}")
_commit=ed41d18ab6b0db386b30ed8ef712ab51a865c7cf  # tags/1.6.4
source=("git+https://anongit.freedesktop.org/git/ModemManager/ModemManager#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ModemManager
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd ModemManager
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ModemManager
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --with-dbus-sys-dir=/usr/share/dbus-1/system.d \
        --with-udev-base-dir=/usr/lib/udev \
        --with-polkit=permissive \
        --with-suspend-resume=no \
        --enable-gtk-doc \
        --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd ModemManager
  make -k check
}

package() {
  cd ModemManager
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C libmm-glib uninstall
  make DESTDIR="$pkgdir" -C vapi uninstall

  # Some stuff to move is left over
  rm -r "$pkgdir/usr/include"
  rm -r "$pkgdir/usr/lib/pkgconfig"
}
