# Maintainer: artoo <artoo@manjaro.org>

_ver=226

pkgname=elogind-dev
pkgver=dev_v${_ver}
pkgrel=1
pkgdesc="The systemd project's logind, extracted to a standalone package"
arch=('i686' 'x86_64')
url="https://github.com/elogind/elogind"
license=('GPL')
provides=("elogind=${_ver}")
depends=('dbus' 'eudev-systemd')
makedepends=('libcap' 'intltool' 'libtool' 'gperf' 'gtk-doc' 'libseccomp' 'eudev')
optdepends=('polkit-elogind: Polkit elogind session tracking')
conflicts=('systemd-sysvcompat' 'elogind' 'consolekit')
options=('!libtool')
install=elogind.install
source=("elogind-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "elogind-docs.patch"
        "elogind.conf"
        "elogind.init")
sha256sums=('165cae0af29ab027f5b1d8286066309aa34d8baa94a5f5e62e8f576b4e43a334'
            '25643e563250b0d322f817288cbe999f1cc2e4804c875725bb92fe5094a902b4'
            '344db76bf8aadfba4363c63aacd93c945a647bd5e425d756cbfd9cb4453afc66'
            'bd00ff5b6f839c8ff12c3b86e1851033b0221851705a6cb0e2242f7c682ce1d4')

prepare() {
    cd ${srcdir}/elogind-${pkgver}
    patch -Np 1 -i ${srcdir}/elogind-docs.patch
    autoreconf -vim
}

build() {
    cd ${srcdir}/elogind-${pkgver}
    intltoolize
    ./configure \
        --sysconfdir=/etc \
        --prefix=/usr \
        --with-rootprefix=/usr \
        --enable-split-usr \
        --enable-smack \
        --disable-kdbus \
        --libexecdir=/usr/lib

    make
}

package() {
    cd ${srcdir}/elogind-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm755 ${srcdir}/elogind.init ${pkgdir}/etc/init.d/elogind
    install -Dm755 ${srcdir}/elogind.conf ${pkgdir}/etc/conf.d/elogind
}
