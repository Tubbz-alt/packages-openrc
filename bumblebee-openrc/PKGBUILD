# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Roland Singer <roland@manjaro.org>
# Maintainer: Philip Mueller <philm@manjaro.org>

_pkgname=bumblebee
_url=https://raw.githubusercontent.com/gentoo/gentoo/master
_commit=0851da6818e265d354d61947021c512f2e51ffce # lastest commit from 20170227

pkgname=bumblebee-openrc
pkgver=3.2.1
pkgrel=11
pkgdesc="NVIDIA Optimus support for Linux through Primus/VirtualGL"
arch=('i686' 'x86_64')
provides=('bumblebee')
depends=('primus' 'glib2' 'mesa-libgl' 'openrc')
makedepends=('help2man' 'libbsd' 'git')
optdepends=('xf86-video-nouveau: nouveau driver'
            'nouveau-dri: 3D acceleration features fo nouveau'
            'bbswitch: switch on/off discrete card'
            'nvidia: NVIDIA kernel driver'
            'virtualgl: alternative back-end for optirun')
optdepends_x86_64=('lib32-virtualgl: run 32bit applications with optirun'
                   'lib32-primus: faster back-end for optirun')
conflicts=('bumblebee' 'bumblebee-pid')
url="http://www.bumblebee-project.org"
license=("GPL3")
backup=('etc/bumblebee/bumblebee.conf'
        'etc/bumblebee/xorg.conf.nouveau'
        'etc/bumblebee/xorg.conf.nvidia'
        'etc/conf.d/bumblebee')
source=("${_pkgname}.zip::https://github.com/FadeMind/Bumblebee/archive/${_commit}.zip"
        "bumblebee.confd::${_url}/x11-misc/bumblebee/files/bumblebee.confd"
        "bumblebee.initd::${_url}/x11-misc/bumblebee/files/bumblebee.initd")
sha256sums=('e50a3948c3320f65cd449a6dbe1c744cfc671a42ccb529a7dc797d1191a3df1c'
            '9e0287f542cff0fd3452a0c3d6d427a3404706c93ac9a00f4d99fe52ed596d75'
            '5c67465ca9a2d4dc6fd35a7827d82ea6d72b13a8e93373e9055970fc71754b18')

_inst_initd(){
	install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

	sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|' \
		-e 's|/var/run|/run|g' \
		-e 's|/usr/sbin|/usr/bin|g' \
		-i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
	install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

build() {
	cd "${srcdir}/Bumblebee-${_commit}"
	autoreconf -fi
	./configure \
		CONF_DRIVER_MODULE_NVIDIA=nvidia \
		CONF_LDPATH_NVIDIA=/usr/lib/nvidia:/usr/lib32/nvidia \
		CONF_MODPATH_NVIDIA=/usr/lib/nvidia/xorg/,/usr/lib/xorg/modules \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--with-udev-rules=/usr/lib/udev/rules.d/ \
		--sysconfdir=/etc \
		--with-pidfile

	make
}

package() {
	cd "${srcdir}/Bumblebee-${_commit}"

	make install DESTDIR="$pkgdir"

	_inst_confd 'bumblebee'
	_inst_initd 'bumblebee'

	# Install udev rule to prevent GH-#144
    install -Dm644 "conf/99-bumblebee-nvidia-dev.rules" "${pkgdir}/usr/lib/udev/rules.d/99-bumblebee-nvidia-dev.rules"

    # Stop nvidia from loading on boot
    mkdir -p "${pkgdir}/etc/modprobe.d"
    echo "remove nvidia modprobe -r --ignore-remove nvidia-drm nvidia-modeset nvidia-uvm nvidia" > "${pkgdir}/etc/modprobe.d/bumblebee.conf"
}
